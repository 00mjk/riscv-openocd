# Proc that can be invoked to create a special version of
# the LPC2148
proc setup_lpc2148 {chipname tapids} {
	# Use RCLK. If RCLK is not available fall back to 500kHz.
	#
	# Depending on cabling you might be able to eek this up to 2000kHz.
	jtag_rclk 500
	
	adapter_nsrst_delay 200
	jtag_ntrst_delay 200
	
	# NOTE!!! LPCs need reset pulled while RTCK is low. 0 to activate
	# JTAG, power-on reset is not enough, i.e. you need to perform a
	# reset before being able to talk to the LPC2148, attach is not possible.
	reset_config trst_and_srst
	
	eval "jtag newtap $chipname cpu -irlen 4 -ircapture 0x1 -irmask 0xf $tapids"
	
	target create $chipname.cpu arm7tdmi -chain-position $chipname.cpu
	
	$chipname.cpu configure -work-area-phys 0x40000000 -work-area-size 0x4000 -work-area-backup 0
	
	$chipname.cpu configure -event reset-init {
		# Force target into ARM state
		arm core_state arm
	
		# Do not remap 0x0000-0x0020 to anything but the flash (i.e. select
		# "User Flash Mode" where interrupt vectors are _not_ remapped,
		# and reside in flash instead).
		#
		# See section 7.1 on page 32 ("Memory Mapping control register") in
		# "UM10139: Volume 1: LPC214x User Manual", Rev. 02 -- 25 July 2006.
		# http://www.standardics.nxp.com/support/documents/microcontrollers/pdf/user.manual.lpc2141.lpc2142.lpc2144.lpc2146.lpc2148.pdf
		mwb 0xE01FC040 0x01
	}
	
	# flash bank <name> lpc2000 <base> <size> 0 0 <target#> <variant> <clock> [calc checksum]
	flash bank $chipname.flash lpc2000 0x0 0x7d000 0 0 $chipname.cpu lpc2000_v2 14765 calc_checksum
}


# Default settings
proc init_targets {} {
	setup_lpc2148 lpc2148 "-expected-id 0x3f0f0f0f -expected-id 0x4f1f0f0f"
}
